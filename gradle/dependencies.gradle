repositories {
    google()
    mavenCentral()
    maven { url 'https://dl.bintray.com/sargunv/maven' }
}

ext {
    fragment_version = '1.3.3'
    koin_version= '3.0.1'
    kotlinCoroutines_version = '1.4.3'
    lifecycle_version = '2.3.1'
    moshi_version = '1.12.0'
    okhttp_version = '4.9.1'
    retrofit_version = '2.9.0'

    mods = [
            base: [
                    usecase: project(":modules:base:usecase"),
                    tests: project(":modules:base:tests"),
            ],
            core: [
                    api: project(":modules:core:api"),
                    domain: project(":modules:core:domain"),
                    resources: project(":modules:core:resources"),
            ],
            feature: [
                    pokemonList: project(':modules:feature:pokemon-list'),
                    pokemonListApi: project(':modules:feature:pokemon-list-api'),
            ]
    ]


    build = [
            compileSdk: 30,
            buildTools: '30.0.3',
            minSdk    : 23,
            targetSdk : 29,
    ]

    deps = [
            kotlin: [
                    reflect: "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
                    stdlib: "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
                    coroutines: "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines_version",
            ],

            android: [
                    activityKtx: 'androidx.activity:activity-ktx:1.1.0',
                    appCompat: 'androidx.appcompat:appcompat:1.2.0',
                    cardView: 'androidx.cardview:cardview:1.0.0',
                    coreKtx: 'androidx.core:core-ktx:1.3.2',
                    constraintLayout: 'androidx.constraintlayout:constraintlayout:1.1.3',
                    coroutines: "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutines_version",
                    fragmentKtx: "androidx.fragment:fragment-ktx:$fragment_version",
                    lifecycleLiveDataKtx: "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version",
                    lifecycleViewModelKtx: "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version",
                    lifecycleViewModelSavedState: "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version",
                    recyclerView: 'androidx.recyclerview:recyclerview:1.2.0',
                    recyclerViewSelection: 'androidx.recyclerview:recyclerview-selection:1.1.0',
                    viewbinding: 'androidx.databinding:viewbinding:4.2.1',
            ],

            koin: [
                    core: "io.insert-koin:koin-core:$koin_version",
                    android: "io.insert-koin:koin-android:$koin_version",
                    androidScope: "io.insert-koin:koin-androidx-scope:$koin_version",
            ],

            media: [
                    coil: 'io.coil-kt:coil:1.2.1',
            ],

            network: [
                    moshi: "com.squareup.moshi:moshi-kotlin:$moshi_version",
                    okhttp3: "com.squareup.okhttp3:okhttp:$okhttp_version",
                    retrofit2: "com.squareup.retrofit2:retrofit:$retrofit_version",
                    retrofit2_moshi: "com.squareup.retrofit2:converter-moshi:$retrofit_version",
            ],
    ]

    kapt = [
            moshi: "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version",
    ]

    testDeps = [
            android: [
                    core: 'androidx.test:core:1.2.0',
                    espresso: 'androidx.test.espresso:espresso-core:3.3.0',
                    junitExt: 'androidx.test.ext:junit:1.1.2',
                    robolectric: 'org.robolectric:robolectric:4.5.1',
            ],

            kotlin: [
                    coroutines: "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutines_version",
            ],

            junit: 'junit:junit:4.13.2',
            mockk: 'io.mockk:mockk:1.11.0',
            mockWebServer: "com.squareup.okhttp3:mockwebserver:$okhttp_version",
            truth: 'com.google.truth:truth:1.1.2',

            koin: "io.insert-koin:koin-test:$koin_version",
    ]
}

// https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.ResolutionStrategy.html
configurations.all {
    resolutionStrategy {
        // kotlin-reflect is a transitive dependency on several direct dependencies
        //  force the version here to avoid adding the dependency directly everywhere
        force deps.kotlin.reflect

        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'org.jetbrains.kotlin') {
                details.useVersion kotlin_version
            }
        }
    }
}
